(include "grid.ddh")

# Initialise the mesh with a primitive box that stretches to resemble a line
(class_function Grid void create () {
	(this.mesh.set_primitive AVDL_PRIMITIVE_LINE)
	(this.mesh.SetTypeLine 1.3)
	(this.mesh.set_colour 0.22 0.19 0.25)

	(this.mesh_big.set_primitive AVDL_PRIMITIVE_LINE)
	(this.mesh_big.SetTypeLine 2.0)
	(this.mesh_big.set_colour 0.22 0.19 0.25)

	(this.mesh_red.set_primitive AVDL_PRIMITIVE_LINE)
	(this.mesh_red.SetTypeLine 2.0)
	(this.mesh_red.set_colour 0.7 0.1 0.1)

	(this.mesh_blue.set_primitive AVDL_PRIMITIVE_LINE)
	(this.mesh_blue.SetTypeLine 2.0)
	(this.mesh_blue.set_colour 0.1 0.1 0.7)
})

(class_function Grid void clean () {
})

(class_function Grid void draw () {
	(int size 20)
	(for (int x 0) (<= x size) (+= x 1) {
		(dd_matrix_push)
		(dd_translatef
			(+ (* size -0.5) (* x 1))
			0
			0
		)
		(dd_rotatef 90 0 1 0)
		(dd_scalef size size size)
		(if (== x (* size 0.5))
			(this.mesh_blue.draw)
		(== (% x 5) (% (/ size 2) 5))
			(this.mesh_big.draw)
			(this.mesh.draw)
		)
		(dd_matrix_pop)
	})
	(for (int y 0) (<= y size) (+= y 1) {
		(dd_matrix_push)
		(dd_translatef
			0
			0
			(+ (* size -0.5) (* y 1))
		)
		(dd_scalef size size size)
		(if (== y (* size 0.5))
			(this.mesh_red.draw)
		(== (% y 5) (% (/ size 2) 5))
			(this.mesh_big.draw)
			(this.mesh.draw)
		)
		(dd_matrix_pop)
	})
})
